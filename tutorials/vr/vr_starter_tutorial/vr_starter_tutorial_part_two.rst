.. _doc_vr_starter_tutorial_part_two:

VR Starter Tutorial Part Two
============================

Introduction
------------

.. image:: img/starter_vr_tutorial_sword.png

This tutorial will show you how to make a beginner VR game project in Godot.

Keep in mind, **one of the most important things when making VR content is getting the scale of your assets correct**!
It can take lots of practice and iterations to get this right, but there are a few things you can do to make it easier:

- In VR, 1 unit is typically considered 1 meter. If you design your assets around that standard, you can save yourself a lot of headache.
- In your 3D modeling program, see if there is a way to measure and use real world distances. In Blender, you can use the MeasureIt add-on; in Maya, you can use the Measure Tool.
- You can make rough models using a tool like `Google Blocks <https://vr.google.com/blocks/>`_, and then refine in another 3D modelling program.
- Test often, as the assets can look dramatically different in VR than on a flat screen!

Throughout the course of this tutorial, we will cover:

- How to tell Godot to run in VR.
- How to make a teleportation locomotion system that uses the VR controllers.
- How to make a artificial movement locomotion system that uses the VR controllers.
- How to create a :ref:`RigidBody <class_RigidBody>`-based system that allows for picking up, dropping, and throwing RigidBody nodes using the VR controllers.
- How to create simple destroyable target.
- How to create some special :ref:`RigidBody <class_RigidBody>`-based objects that can destroy the targets.

.. tip:: While this tutorial can be completed by beginners, it is highly
          advised to complete :ref:`doc_your_first_game`,
          if you are new to Godot and/or game development.
          
          **Some experience with making 3D games is required** before going through this tutorial series.
          This tutorial assumes you have experience with the Godot editor, GDScript, and basic 3D game development.
          A OpenVR-ready headset and two OpenVR-ready controllers are required.
          
          This tutorial was written and tested using a Windows Mixed Reality headset and controllers. This project has also been tested on the HTC Vive. Code adjustments may be required
          for other VR Headsets, such as the Oculus Rift.

The Godot project for this tutorial is found on the `OpenVR GitHub repository <https://github.com/GodotVR/godot_openvr_fps>`_. The starter assets for this tutorial can be found in the releases
section on the GitHub repository. The starter assets contain some 3D models, sounds, scripts, and scenes that are configured for this tutorial.

.. note:: **Credits for the assets provided**:
          
          - The sky panorama was created by `CGTuts <https://cgi.tutsplus.com/articles/freebie-8-awesome-ocean-hdris--cg-5684>`_.
          
          - The font used is Titillium-Regular 
          - - The font is licensed under the SIL Open Font License, Version 1.1
          
          - The audio used are from several different sources, all downloaded from the Sonniss #GameAudioGDC Bundle (`License PDF <https://sonniss.com/gdc-bundle-license/>`_) 
          - - The folders where the audio files are stored have the same name as folders in the Sonniss audio bundle.
          
          - The OpenVR addon was created by `Bastiaan Olij <https://github.com/BastiaanOlij>`_ and is released under the MIT license. It can be found both on the `Godot Asset Library <https://godotengine.org/asset-library/asset/150>`_ and on `GitHub <https://github.com/GodotVR/godot-openvr-asset>`_.
          
          - The initial project, 3D models, and scripts were created by `TwistedTwigleg <https://github.com/TwistedTwigleg>`_ and is released under the MIT license.

.. tip:: You can find the finished project on the `OpenVR GitHub repository <https://github.com/GodotVR/godot_openvr_fps>`_.


Adding destroyable targets
--------------------------

Firstly, let's start by making some targets we will destroy in various ways with various special :ref:`RigidBody <class_RigidBody>` nodes.

Open up ``Sphere_Target.tscn``, which you can find in the ``Scenes`` folder. ``Sphere.tscn`` is just a :ref:`StaticBody <class_StaticBody>`
with a :ref:`CollisionShape <class_CollisionShape>`, a mesh, and a audio player.

Select the ``Sphere_Target`` root node, the :ref:`StaticBody <class_StaticBody>` node, and make a new script called ``Sphere_Target.gd``. Add the following
to ``Sphere_Target.gd``:

.. tabs::
 .. code-tab:: gdscript GDScript

    extends StaticBody

    var destroyed = false
    var destroyed_timer = 0
    const DESTROY_WAIT_TIME = 80

    var health = 80

    const RIGID_BODY_TARGET = preload("res://Assets/RigidBody_Sphere.scn")

    func _ready():
        set_physics_process(false)

    func _physics_process(delta):
        destroyed_timer += delta
        if destroyed_timer >= DESTROY_WAIT_TIME:
            queue_free()


    func damage(bullet_global_transform, damage):

        if destroyed:
            return

        health -= damage

        if health <= 0:

            $CollisionShape.disabled = true
            $Sphere_Target.visible = false

            var clone = RIGID_BODY_TARGET.instance()
            add_child(clone)
            clone.global_transform = global_transform

            destroyed = true
            set_physics_process(true)

            $AudioStreamPlayer.play()
            get_tree().root.get_node("Game").remove_sphere()

Let's go over how this script works, starting with the class variables.

- ``destroyed``: A variable to track whether this target is destroyed or not.
- ``destroyed_timer``: A variable to track how long the target has been destroyed.
- ``DESTROY_WAIT_TIME``: A constant to tell the sphere target how long to wait before destroying/deleting itself.
- ``health``: The amount of health the target has.
- ``RIGID_BODY_TARGET``: The target broken into several smaller :ref:`RigidBody <class_RigidBody>` nodes.

________

Let's go over ``_ready``.

All we are doing in ``_ready`` is setting ``_physics_process`` to ``false``. This is because we will only use ``_physics_process``
for destroying the target, so we do not want to call it until the target is broken.

________

Next, let's go over ``_physics_process``.

Firstly, we add time to ``destroyed_timer``. Then we check to see whether enough time has passed and we can destroy the target. If enough time has
passed, we free/destroy the target using ``queue_free``.

________

Finally, let's go over ``damage``.

Firstly, we check to make sure the target has not already been destroyed.

Then, we remove however much damage the target has taken from the target's health.

If the target has zero or less health, then it has taken enough damage to break.

Firstly, we disable the collision shape and make the whole target mesh invisible.
Next, we spawn/instance the :ref:`RigidBody <class_RigidBody>` version of the target, and instance it at this target's position.

Then, we set ``destroyed`` to ``true`` and start processing ``_physics_process``.
Finally, we play a sound, and remove a sphere from ``Game.gd`` by calling ``remove_sphere``.

________

Now, you may have noticed we are calling a function in ``Game.gd`` we have not made yet, so let's fix that!

Firstly, open up ``Game.gd`` and add the following additional class variables:

.. tabs::
 .. code-tab:: gdscript GDScript

    var spheres_left = 10
    var sphere_ui = null

- ``spheres_left``: The amount of sphere targets left in the game world.
- ``sphere_ui``: A reference to the sphere UI. We will use this later!

Next, we need to add the ``remove_sphere`` function. Add the following to ``Game.gd``:

.. tabs::
 .. code-tab:: gdscript GDScript

    func remove_sphere():
        spheres_left -= 1

        if sphere_ui:
            sphere_ui.update_ui(spheres_left)

What this function does is it subtracts one from ``spheres_left``.

Then, it checks to see whether ``sphere_ui`` is not null, and if it is not, then it calls its ``update_ui`` function, passing in the amount of spheres left.
We'll add the UI code later in this part.

Now that we have destroyable targets, we need a way to destroy them!

Adding a pistol
---------------

Okay, let's add a pistol. Open up ``Pistol.tscn``, which you will find in the ``Scenes`` folder.

There are a few things to note here. The first thing to note is how everything is rotated. This is to make the pistol rotate correctly when the player grabs it. The other thing to notice is
how there is a laser sight mesh, and a flash mesh; both of these do what you'd expect: act as a laser pointer and muzzle flash, respectively.

The other thing to notice is how there is a :ref:`Raycast <class_Raycast>` node at the end of the pistol. This is what we will be using to calculate where the bullets impact.

Now that we have looked at the scene, let's write the code. Select the ``Pistol`` root node, the :ref:`RigidBody <class_RigidBody>` node, and make a new
script called ``Pistol.gd``. Add the following code to ``Pistol.gd``:

.. tabs::
 .. code-tab:: gdscript GDScript

    extends RigidBody

    onready var flash_mesh = $Pistol_Flash
    onready var laser_sight_mesh = $LaserSight
    onready var raycast = $RayCast

    const FLASH_TIME = 0.25
    var flash_timer = 0

    var BULLET_DAMAGE = 20

    func _ready():
        flash_mesh.visible = false
        laser_sight_mesh.visible = false

    func _physics_process(delta):
        if flash_timer > 0:
            flash_timer -= delta
            # If the flash has been visible enough, then make the flash mesh invisible.
            if flash_timer <= 0:
                flash_mesh.visible = false


    # Called when the interact button is pressed while the object is held.
    func interact():

        if flash_timer <= 0:

            flash_timer = FLASH_TIME
            flash_mesh.visible = true

            raycast.force_raycast_update()
            if raycast.is_colliding():

                var body = raycast.get_collider()

                if body.has_method("damage"):
                    body.damage(raycast.global_transform, BULLET_DAMAGE)
                elif body.has_method("apply_impulse"):
                    var direction_vector = raycast.global_transform.basis.z.normalized()
                    body.apply_impulse((raycast.global_transform.origin - body.global_transform.origin).normalized(), direction_vector * 1.2)

            $AudioStreamPlayer3D.play()


    # Called when the object is picked up.
    func picked_up():
        laser_sight_mesh.visible = true


    # Called when the object is dropped.
    func dropped():
        laser_sight_mesh.visible = false

Let's go over what this script does, starting with the class variables:

- ``flash_mesh``: The mesh used to make the muzzle flash.
- ``FLASH_TIME``: The length of time the muzzle flash is visible.
- ``flash_timer``: A variable to track how long the muzzle flash has been visible.
- ``laser_sight_mesh``: A long rectangular mesh used for the laser sight.
- ``raycast``: The raycast node used for the pistol firing.
- ``BULLET_DAMAGE``: The amount of damage a single bullet does.

________

Let's go over ``_ready``.

All we are doing here is getting the nodes and assigning them to the proper variables. We also make sure the flash and laser
sight meshes are invisible.

________

Next, let's look at ``_physics_process``.

Firstly, we check to see if the flash is visible. We do this by checking to see if ``flash_timer`` is more than zero. This is because ``flash_timer`` will be an inverted timer,
a timer that counts down instead of counting up.

If ``flash_timer`` is more than zero, we subtract ``delta`` from it and check to see whether it is equal to zero or less.
If it is, we make the flash mesh invisible.

This makes it where the flash mesh becomes invisible after ``FLASH_TIME`` many seconds have gone by.

________

Now, let's look at ``interact``, which is called when the trigger button on the VR controller is pressed and the pistol is being held.

Firstly, we check to see if the flash timer is less than or equal to zero. This check makes it where we cannot fire when the flash is visible, limiting how often
the pistol can fire.

If we can fire, we reset ``flash_timer`` by setting it to ``FLASH_TIME``, and we make the flash mesh visible.

We then update the :ref:`Raycast <class_Raycast>` and check to see if it is colliding with anything.

If the :ref:`Raycast <class_Raycast>` is colliding with something, we get the collider. We check to see if the collider has the ``damage`` function, and if it does, we call it.
If it does not, we then check to see if the collider has the ``apply_impulse`` function, and if it does, we call it after calculating the direction from the
:ref:`Raycast <class_Raycast>` to the collider.

Finally, regardless of whether the pistol hit something or not, we play the pistol firing sound.

________

Finally, let's look at ``picked_up`` and ``dropped``, which are called when the pistol is picked up and dropped, respectively.

All we are doing in these functions is making the laser pointer visible when the pistol is picked up, and making it invisible when the pistol is dropped.

________

.. image:: img/starter_vr_tutorial_pistol.png

With that done, go ahead and give the game a try! If you climb up the stairs and grab the pistols, you should be able to fire at the spheres and they will break!

Adding a shotgun
----------------

Let's add a different type of weapon :ref:`RigidBody <class_RigidBody>`: a shotgun. This is fairly straightforward, as almost everything is the same as the pistol.

Open up ``Shotgun.tscn``, which you can find in ``Scenes``. Notice how everything is more or less the same, but instead of a single :ref:`Raycast <class_Raycast>`,
there are five, and there is no laser pointer.
This is because a shotgun generally fires in a cone shape, and so we are going to emulate that by having several :ref:`Raycast <class_Raycast>` nodes, all rotated randomly
in a cone shape, and I removed the laser pointer so the player has to aim without knowing for sure where the shotgun is pointing.

Alright, select the ``Shotgun`` root node, the :ref:`RigidBody <class_RigidBody>` and make a new script called ``Shotgun.gd``. Add the following to ``Shotgun.gd``:

.. tabs::
 .. code-tab:: gdscript GDScript

    extends RigidBody

    onready var flash_mesh = $Shotgun_Flash
    onready var raycasts = $Raycasts

    const FLASH_TIME = 0.25
    var flash_timer = 0

    var BULLET_DAMAGE = 30

    func _ready():
        flash_mesh.visible = false

    func _physics_process(delta):
        if flash_timer > 0:
            flash_timer -= delta
            if flash_timer <= 0:
                flash_mesh.visible = false


    # Called when the interact button is pressed while the object is held.
    func interact():

        if flash_timer <= 0:

            flash_timer = FLASH_TIME
            flash_mesh.visible = true

            for raycast in raycasts.get_children():

                raycast.rotation_degrees = Vector3(90 + rand_range(10, -10), 0, rand_range(10, -10))

                raycast.force_raycast_update()
                if raycast.is_colliding():

                    var body = raycast.get_collider()

                    # If the body has the damage method, then use that; otherwise, use apply_impulse.
                    if body.has_method("damage"):
                        body.damage(raycast.global_transform, BULLET_DAMAGE)
                    elif body.has_method("apply_impulse"):
                        var direction_vector = raycast.global_transform.basis.z.normalized()
                        body.apply_impulse((raycast.global_transform.origin - body.global_transform.origin).normalized(), direction_vector * 4)

            $AudioStreamPlayer3D.play()


    func picked_up():
        pass


    func dropped():
        pass

You may have noticed this is almost exactly the same as the pistol, and indeed it is, so let's only go over what has changed.

- ``raycasts``: The node that holds all of the five :ref:`Raycast <class_Raycast>` nodes used for the shotgun's firing.

In ``_ready``, we get the ``Raycasts`` node, instead of just a single :ref:`Raycast <class_Raycast>`.

The only other change, besides there being nothing in ``picked_up`` and ``dropped``, is in ``interact``.

Now we go through each :ref:`Raycast <class_Raycast>` in ``raycasts``. We then rotate it on the X and Z axes, making within a 10 to ``-10`` cone.
From there, we process each :ref:`Raycast <class_Raycast>` like we did the single :ref:`Raycast <class_Raycast>` in the pistol, nothing changed at all,
we are just doing it five times, once for each :ref:`Raycast <class_Raycast>` in ``raycasts``.

________

Now you can find and fire the shotgun too! The shotgun is located around the back behind one of the walls (not in the building though!).

Adding a bomb
-------------

While both of those are well and good, let's add something we can throw next â€” a bomb!

Open up ``Bomb.tscn``, which you will find in the ``Scenes`` folder.

First, notice how there is a rather large :ref:`Area <class_Area>` node. This is the explosion radius for the bomb. Anything within this :ref:`Area <class_Area>` will be
effected by the explosion when the bomb explodes.

The other thing to note is how there are two sets of :ref:`Particles <class_Particles>`: one for smoke coming out of the fuse, and another for the explosion itself.
Feel free to take a look at the :ref:`Particles <class_Particles>` nodes if you want!

The only thing to notice is how long the explosion :ref:`Particles <class_Particles>` node will last, their lifetime, which is 0.75 seconds. We need to know this so we can time
the removal of the bomb with the end of the explosion :ref:`Particles <class_Particles>`.

Alright, now let's write the code for the bomb. Select the ``Bomb`` :ref:`RigidBody <class_RigidBody>` node and make a new script called ``Bomb.gd``. Add the following code to
``Bomb.gd``:

.. tabs::
 .. code-tab:: gdscript GDScript

    extends RigidBody

    onready var bomb_mesh = $Bomb
    onready var explosion_area = $Area
    onready var fuse_particles = $Fuse_Particles
    onready var explosion_particles = $Explosion_Particles

    const FUSE_TIME = 4
    var fuse_timer = 0

    var EXPLOSION_DAMAGE = 100
    var EXPLOSION_TIME = 0.75
    var explosion_timer = 0
    var explode = false
    var controller = null

    func _ready():
        set_physics_process(false)

    func _physics_process(delta):

        if fuse_timer < FUSE_TIME:

            fuse_timer += delta

            if fuse_timer >= FUSE_TIME:

                fuse_particles.emitting = false
                explosion_particles.one_shot = true
                explosion_particles.emitting = true
                bomb_mesh.visible = false

                collision_layer = 0
                collision_mask = 0
                mode = RigidBody.MODE_STATIC

                for body in explosion_area.get_overlapping_bodies():
                    if body == self:
                        pass
                    else:
                        if body.has_method("damage"):
                            body.damage(global_transform.looking_at(body.global_transform.origin, Vector3(0, 1, 0)), EXPLOSION_DAMAGE)
                        elif body.has_method("apply_impulse"):
                            var direction_vector = body.global_transform.origin - global_transform.origin
                            body.apply_impulse(direction_vector.normalized(), direction_vector.normalized() * 1.8)

                explode = true
                $AudioStreamPlayer3D.play()


        if explode:

            explosion_timer += delta
            if explosion_timer >= EXPLOSION_TIME:

                explosion_area.monitoring = false

                if controller:
                    controller.held_object = null
                    controller.hand_mesh.visible = true

                    if controller.grab_mode == "RAYCAST":
                        controller.grab_raycast.visible = true

                queue_free()


    func interact():
        set_physics_process(true)
        fuse_particles.emitting = true


    func picked_up():
        pass

    func dropped():
        pass

Let's go through what this script does, starting with the class variables:

- ``bomb_mesh``: The :ref:`MeshInstance <class_MeshInstance>` used for the bomb mesh.
- ``FUSE_TIME``: The length of time for which the fuse burns.
- ``fuse_timer``: A variable for tracking how long the fuse has been burning.
- ``explosion_area``: The :ref:`Area <class_Area>` node used for detecting what nodes are inside the explosion.
- ``EXPLOSION_DAMAGE``: The amount of damage the explosion does.
- ``EXPLOSION_TIME``: The length of time the explosion :ref:`Particles <class_Particles>` take (you can calculate this number by multiplying the particles ``lifetime`` by its ``speed scale``)
- ``explosion_timer``: A variable for tracking how long the explosion has lasted.
- ``explode``: A boolean for tracking whether the bomb has exploded.
- ``fuse_particles``: The fuse :ref:`Particles <class_Particles>` node.
- ``explosion_particles``: The explosion :ref:`Particles <class_Particles>` node.
- ``controller``: The controller that is currently holding the bomb, if there is one. This is set by the controller, so we do not need to check anything outside of checking if it is ``null``.

________

Let's go through ``_ready``.

Firstly, we get all the nodes and assign them to the proper variables for later use.

Then, we make sure ``_physics_process`` is not going to be called. We do this since we will be using ``_physics_process`` only for the fuse and
for destroying the bomb, so we do not want to trigger that early, we only want the fuse to start when the player interacts while holding a bomb.

________

Now, let's look at ``_physics_process``.

Firstly we check to see whether ``fuse_timer`` is less than ``FUSE_TIME``. If ``fuse_timer`` is less than ``FUSE_TIME``, then the bomb must be burning down the fuse.

We then add time to ``fuse_timer``, and check to see whether the bomb has waited long enough and has burned through the entire fuse.

If the bomb has waited long enough, then we need to explode the bomb. We do this first by stopping the smoke :ref:`Particles <class_Particles>` from emitting, and
making the explosion :ref:`Particles <class_Particles>` emit. We also hide the bomb mesh so it is no longer visible.

Next, we make the set the collision layer and mask to zero, and set the :ref:`RigidBody <class_RigidBody>` mode to static. This makes it where the now exploded bomb cannot
interact with the physics world, and so it will stay in place.

Then, we go through everything inside the explosion :ref:`Area <class_Area>`. We make sure the bodies inside the explosion :ref:`Area <class_Area>` are not the bomb itself, since we
do not want to explode the bomb with itself. We then check to see whether the bodies have the ``damage`` method/function, and if it does, we call that, while if it does not, we check to
see if it has the ``apply_impulse`` method/function, and call that instead.

Then, we set ``explode`` to true since the bomb has exploded, and we play a sound.

Next, we check to see if the bomb has exploded, as we need to wait until the explosion :ref:`Particles <class_Particles>` are done.

If the bomb has exploded, we add time to ``explosion_timer``. We then check to see if the explosion :ref:`Particles <class_Particles>` are done. If they are, we set the explosion
:ref:`Area <class_Area>`'s monitoring property to ``false`` to ensure we do not get any bugs in the debugger, we make the controller drop the bomb if it is holding onto it,
we make the grab :ref:`Raycast <class_Raycast>` visible if the grab mode is ``RAYCAST``, and we free/destroy the bomb using ``queue_free``.

________

Finally, let's look at ``interact``.

All we are doing here is making it where ``_physics_process`` will be called, which will start the fuse.
We also make the fuse :ref:`Particles <class_Particles>` start emitting, so smoke comes out the top of the bomb.

________

With that done, the bombs are ready to go! You can find them in the orange building. Because of how we are calculating velocity, it is easiest to throw bombs in a trusting-like
motion as opposed to a more natural throwing like motion. The smooth curve of a throwing-like motion is harder to track, and the because of how we are tracking velocity, it does
not always work.

Adding a sword
--------------

Finally, let's add a sword so we can slice through things!

Open up ``Sword.tscn``, which you will find in ``Scenes``.

There is not a whole lot to note here, but there is just one thing, and that is how the length of the blade of the sword is broken into several small :ref:`Area <class_Area>` nodes.
This is because we need to roughly know where on the blade the sword collided, and this is the easiest (and only) way I could figure out how to do this.

.. tip:: If you know how to find the point where an :ref:`Area <class_Area>` and a :ref:`CollisionObject <class_CollisionObject>` meet, please let me know and/or make a PR on the
         Godot documentation! This method of using several small :ref:`Area <class_Area>` nodes works okay, but it is not ideal.

Other than that, there really is not much of note, so let's write the code. Select the ``Sword`` root node, the :ref:`RigidBody <class_RigidBody>` and make a new script called
``Sword.gd``. Add the following code to ``Sword.gd``:

.. tabs::
 .. code-tab:: gdscript GDScript

    extends RigidBody

    const SWORD_DAMAGE = 20

    var controller

    func _ready():
        $Damage_Area_01.connect("body_entered", self, "body_entered_sword", ["01"])
        $Damage_Area_02.connect("body_entered", self, "body_entered_sword", ["02"])
        $Damage_Area_03.connect("body_entered", self, "body_entered_sword", ["03"])
        $Damage_Area_04.connect("body_entered", self, "body_entered_sword", ["04"])


    # Called when the interact button is pressed while the object is held.
    func interact():
        pass


    # Called when the object is picked up.
    func picked_up():
        pass


    # Called when the object is dropped.
    func dropped():
        pass


    func body_entered_sword(body, number):
        if body == self:
            pass
        else:

            var sword_part = null
            if number == "01":
                sword_part = get_node("Damage_Area_01")
            elif number == "02":
                sword_part = get_node("Damage_Area_02")
            elif number == "03":
                sword_part = get_node("Damage_Area_03")
            elif number == "04":
                sword_part = get_node("Damage_Area_04")

            if body.has_method("damage"):
                body.damage(sword_part.global_transform.looking_at(body.global_transform.origin, Vector3(0, 1, 0)), SWORD_DAMAGE)

                get_node("AudioStreamPlayer3D").play()

           elif body.has_method("apply_impulse"):

                var direction_vector = sword_part.global_transform.origin - body.global_transform.origin

                if not controller:
                    body.apply_impulse(direction_vector.normalized(), direction_vector.normalized() * self.linear_velocity)
                else:
                    body.apply_impulse(direction_vector.normalized(), direction_vector.normalized() * controller.controller_velocity)

                $AudioStreamPlayer3D.play()

Let's go over what this script does, starting with the two class variables:

- ``SWORD_DAMAGE``: The amount of damage a single sword slice does.
- ``controller``: The controller that is holding the sword, if there is one. This is set by the controller, so we do not need to set it here, in ``Sword.gd``.

________

Let's go over ``_ready`` next.

All we are doing here is connecting each of the :ref:`Area <class_Area>` nodes ``body_entered`` signal to the ``body_entered_sword`` function, passing in an additional argument,
which will be the number of the damage :ref:`Area <class_Area>`, so we can figure out where on the sword the body collided.

________

Now let's go over ``body_entered_sword``.

Firstly, we make sure the body the sword has collided with is not itself.

Then we figure out which part of the sword the body collided with, using the passed-in number.

Next, we check to see whether the body the sword collided with has the ``damage`` function, and if it does, we call it and play a sound.

If it does not have the damage function, we then check to see whether it has the ``apply_impulse`` function. If it does, we then calculate the direction from the sword part the
body collided with to the body. We then check to see whether the sword is being held or not.

If the sword is not being held, we use the :ref:`RigidBody <class_RigidBody>`'s velocity as the force in ``apply_impulse``, while if the sword is being held, we use the
controller's velocity as the force in the impulse.

Finally, we play a sound.

________

.. image:: img/starter_vr_tutorial_sword.png

With that done, you can now slice through the targets! You can find the sword in the corner in between the shotgun and the pistol.

Updating the target UI
----------------------

Okay, let's update the UI as the sphere targets are destroyed.

Open up ``Game.tscn`` and then expand the ``GUI`` :ref:`MeshInstance <class_MeshInstance>`. From there, expand the ``GUI`` :ref:`Viewport <class_Viewport>` node
and then select the ``Base_Control`` node. Add a new script called ``Base_Control``, and add the following:

.. tabs::
 .. code-tab:: gdscript GDScript

    extends Control

    var sphere_count_label = $Label_Sphere_Count

    func _ready():
        get_tree().root.get_node("Game").sphere_ui = self

    func update_ui(sphere_count):
        if sphere_count > 0:
            sphere_count_label.text = str(sphere_count) + " Spheres remaining"
        else:
            sphere_count_label.text = "No spheres remaining! Good job!"

Let's go over what this script does.

First, in ``_ready``, we get the :ref:`Label <class_Label>` that shows how many spheres are left and assign it to the ``sphere_count_label`` class variable.
Next, we get ``Game.gd`` by using ``get_tree().root`` and assign ``sphere_ui`` to this script.

In ``update_ui``, we change the sphere :ref:`Label <class_Label>`'s text. If there is at least one sphere remaining, we change the text to show how many spheres are still
left in the world. If there are no more spheres remaining, we change the text and congratulate the player.

Adding the final special RigidBody
----------------------------------

Finally, before we finish this tutorial, let's add a way to reset the game while in VR.

Open up ``Reset_Box.tscn``, which you will find in ``Scenes``. Select the ``Reset_Box`` :ref:`RigidBody <class_RigidBody>` node and make a new script called ``Reset_Box.gd``.
Add the following code to ``Reset_Box.gd``:

.. tabs::
 .. code-tab:: gdscript GDScript

    extends RigidBody

    var start_transform

    var reset_timer = 0
    const RESET_TIME = 120


    func _ready():
        start_transform = global_transform


    func _physics_process(delta):
        reset_timer += delta
        if reset_timer >= RESET_TIME:
            global_transform = start_transform
            reset_timer = 0


    # Called when the interact button is pressed while the object is held.
    func interact():
        get_tree().change_scene("res://Game.tscn")


    # Called when the object is picked up.
    func picked_up():
        pass


    # Called when the object is dropped.
    func dropped():
        global_transform = start_transform
        reset_timer = 0

Let's go over what this does.

First, we get the starting global :ref:`Transform <class_Transform>` in ``_ready``, and assign it to ``start_transform``. We will use this to reset the position of the reset box every so often.

In ``_physics_process``, we check to see if enough time has passed to reset. If it has, we reset the box's :ref:`Transform <class_Transform>` and then reset the timer.

If the player interacts while holding the reset box, we reset the scene by calling ``get_tree().change_scene`` and passing in the path to the current scene. This resets/restarts
the scene completely.

When the reset box is dropped, we reset the :ref:`Transform <class_Transform>` and timer.

________

With that done, when you grab and interact with the reset box, the entire scene will reset/restart and you can destroy all the targets again!

Final notes
-----------

.. image:: img/starter_vr_tutorial_sword.png

Whew! That was a lot of work. Now you have a  fully working VR project!

.. warning:: You can download the finished project for this tutorial series on the Godot OpenVR GitHub repository, under the releases tab!

This will hopefully serve as an introduction to making fully-featured VR games in Godot! The code written here can be expanded to make puzzle games, action games,
story-based games, and more!
